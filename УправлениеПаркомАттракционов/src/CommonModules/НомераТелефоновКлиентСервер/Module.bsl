
#Область ПрограммныйИнтерфейс

// Преобразует номер телефона к нормализованному виду
// 
// Параметры:
//  НомерТелефона - Строка - Номер телефона
//  ВызыватьИсключение - Булево - Необходимость вызывать исключение при наличии ошибок
// 
// Возвращаемое значение:
//  Строка - Нормализованный номер телефона
Функция НормализованныйНомерТелефона(Знач НомерТелефона = "", ВызыватьИсключение = Ложь) Экспорт
	
	НормализованныйНомер = "";
	Ошибки = Новый Массив;
	
	Цифры = "0123456789";
	
	Для Сч = 1 по СтрДлина(НомерТелефона) Цикл
		Символ = Сред(НомерТелефона, Сч, 1);
		
		Если СтрНайти(Цифры, Символ) = 0 Тогда	
			Продолжить;			
		КонецЕсли;		
			
		НормализованныйНомер = СтрШаблон("%1%2", НормализованныйНомер, Символ);	
	КонецЦикла;	
	
	Если СтрНачинаетсяС(НормализованныйНомер, "8") Тогда	
		НормализованныйНомер = "7" + Сред(НормализованныйНомер, 2);	
	КонецЕсли;	

	Если Не СтрНачинаетсяС(НормализованныйНомер, "7") Тогда	
		ТекстСообщения = "Первая цифра телефона должна быть 7 или 8";
		
		Ошибки.Добавить(ТекстСообщения);		
	КонецЕсли;	
	
	Если Не СтрДлина(НормализованныйНомер) = 11 Тогда
		ТекстСообщения = "Номер телефона должен содержать 11 цифр";
		
		Ошибки.Добавить(ТекстСообщения);		
	КонецЕсли;
		
	Если Ошибки.Количество() Тогда
		Если ВызыватьИсключение Тогда
			ВызватьИсключение СтрСоединить(Ошибки, "; ");
		Иначе
			НормализованныйНомер = "";
		КонецЕсли;			
	КонецЕсли;		
	
	Возврат НормализованныйНомер;	
	
КонецФункции	
	
#КонецОбласти
