#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)
	//{{__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
	//Данный фрагмент построен конструктором.
	//При повторном использовании конструктора, внесенные вручную данные будут утеряны!

	// регистр АктуальнаяШкалаБонуснойПрограммы
	Движения.АктуальнаяШкалаБонуснойПрограммы.Записывать = Истина;
	Движение = Движения.АктуальнаяШкалаБонуснойПрограммы.Добавить();
	Движение.Период = Дата;
	Движение.Шкала = Ссылка;

	//}}__КОНСТРУКТОР_ДВИЖЕНИЙ_РЕГИСТРОВ
КонецПроцедуры


Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Не Диапазоны.Количество() Тогда
		Возврат;		
	КонецЕсли;	
	
	//1. Нижняя граница первого диапазона должна быть = 0
	Если Диапазоны[0].НижняяГраница > 0 Тогда	
		Отказ = Истина;
		
		ТекстСообщения = "Нижняя граница в первой строке должна быть = 0";
		Поле = СтрШаблон("Диапазоны[%1].НижняяГраница", 0);
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Поле);
	КонецЕсли;	

	ПредыдущаяГраница = 0;	
	
	Для Каждого СтрокаТаблицы из Диапазоны Цикл		
		//2. Верхняя граница каждого диапазона, кроме последнего, должна быть = нижней границе следующего
		Если СтрокаТаблицы.НомерСтроки > 1 
			И Не СтрокаТаблицы.НижняяГраница = ПредыдущаяГраница Тогда		
			Отказ = Истина; 
			ТекстСообщения = "Нижняя граница последующего диапазона должна быть равна верхней границы предыдущего";
			Поле = СтрШаблон("Диапазоны[%1].НижняяГраница", Диапазоны.Индекс(СтрокаТаблицы));
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Поле);
		КонецЕсли;					

		//3. Верхняя граница всегда больше нижней границы (кроме последнего диапазона)
		Если СтрокаТаблицы.НомерСтроки < Диапазоны.Количество()
				И СтрокаТаблицы.НижняяГраница >= СтрокаТаблицы.ВерхняяГраница
		Тогда
			Отказ = Истина;
			ТекстСообщения = "Верхняя граница диапазона всегда должна быть больше нижней (кроме последней строки)";
			Поле = СтрШаблон("Диапазоны[%1].ВерхняяГраница", Диапазоны.Индекс(СтрокаТаблицы));
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Поле);
		КонецЕсли;			

		//4. Верхняя граница последнего диапазона должна быть = 0
		Если СтрокаТаблицы.НомерСтроки = Диапазоны.Количество() 
				И Не СтрокаТаблицы.ВерхняяГраница = 0
		Тогда
			Отказ = Истина;
			ТекстСообщения = "Верхняя граница последнего диапазона должна быть равна нулю";
			Поле = СтрШаблон("Диапазоны[%1].ВерхняяГраница", Диапазоны.Индекс(СтрокаТаблицы));
		
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Поле);
		КонецЕсли;
					
		ПредыдущаяГраница = СтрокаТаблицы.ВерхняяГраница; 
	КонецЦикла;	
	
КонецПроцедуры
#КонецОбласти


#КонецЕсли
