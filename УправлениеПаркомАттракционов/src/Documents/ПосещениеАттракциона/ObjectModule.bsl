#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ,Режим)

	ВидАттракционаОснования = ВидАттракционаОснования(Основание);
	
	// регистр ПосещенияАттракционов
	Движения.ПосещенияАттракционов.Записывать = Истина;
	Движение = Движения.ПосещенияАттракционов.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Основание = Основание;
	Движение.ВидАттракциона = ВидАттракционаОснования;
	Движение.КоличествоПосещений = 1;

	// Принудительная запись для контроля остатков
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПосещенияАттракционовОстатки.Основание КАК Основание,
		|	ПосещенияАттракционовОстатки.ВидАттракциона КАК ВидАттракциона,
		|	ПосещенияАттракционовОстатки.КоличествоПосещенийОстаток КАК КоличествоПосещенийОстаток
		|ИЗ
		|	РегистрНакопления.ПосещенияАттракционов.Остатки(&Период, Основание = &Основание) КАК ПосещенияАттракционовОстатки
		|ГДЕ
		|	ПосещенияАттракционовОстатки.КоличествоПосещенийОстаток < 0
		|";
	
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = "Не куплен билет, либо билет был использован ранее";	
		Сообщение.Поле = Основание;
		Сообщение.Сообщить();
	КонецЕсли;	

КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Функция ВидАттракционаОснования(Основание)
	
	ВидАттракционаОснования = Справочники.ВидыАттракционов.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПродажаБилета.Номенклатура.ВидАттракциона КАК ВидАттракциона
		|ИЗ
		|	Документ.ПродажаБилета КАК ПродажаБилета
		|ГДЕ
		|	ПродажаБилета.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Основание);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ВидАттракционаОснования = ВыборкаДетальныеЗаписи.ВидАттракциона;			
	КонецЕсли;
	
	Возврат ВидАттракционаОснования;
	
КонецФункции	

#КонецОбласти

#КонецЕсли
