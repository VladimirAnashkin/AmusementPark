
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)

	Объект.Цена = ЦенаНоменклатуры(Объект.Номенклатура, Объект.Дата);
	
	РассчитатьСуммуДокумента();	
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();

КонецПроцедуры

&НаКлиенте
Процедура БаллыКСписаниюПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	РассчитатьСкидку();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЦенаНоменклатуры(Знач Номенклатура, Знач Дата)

	ЦенаНоменклатуры = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЦеныНоменклатурыСрезПоследних.Цена КАК Цена
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Период, Номенклатура = &Номенклатура) КАК
		|		ЦеныНоменклатурыСрезПоследних";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЦенаНоменклатуры = ВыборкаДетальныеЗаписи.Цена;
	КонецЕсли;
	
	Возврат ЦенаНоменклатуры;

КонецФункции

&НаКлиенте
Процедура РассчитатьСкидку()
	
	Объект.БаллыКСписанию = Объект.Цена - Объект.СуммаДокумента;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Цена - Объект.БаллыКСписанию;
	
КонецПроцедуры

#КонецОбласти
