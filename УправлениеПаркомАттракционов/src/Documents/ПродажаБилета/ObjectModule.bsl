#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	
	Если БаллыКСписанию > 0 Тогда 
	
		Если БаллыКСписанию > Цена Тогда
			Отказ = Истина;
			ТекстСообщения = СтрШаблон("Списанные балы:%1 не должны превышать цену билета:%2", БаллыКСписанию, Цена);
		
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле = "БаллыКСписанию";
			Сообщение.Сообщить();
		КонецЕсли;

		Если Цена > 0 Тогда
			МаксимальнаяДоля = Константы.МаксимальнаяДоляОплатыБаллами.Получить();
			Доля = БаллыКСписанию / Цена * 100;
			
			Если Доля > МаксимальнаяДоля Тогда
				Отказ = Истина;
				ТекстСообщения = СтрШаблон("Доля списанных балов:%1%% больше допустимой:%2%%", Доля, МаксимальнаяДоля);
		
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = ТекстСообщения;
				Сообщение.Поле = "БаллыКСписанию";
				Сообщение.Сообщить();
			КонецЕсли;	
		КонецЕсли;
	КонецЕсли;		

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ,Режим)

	СвойстваНоменклатуры = СвойстваНоменклатуры();

	// регистр ПосещенияАттракционов
	Движения.ПосещенияАттракционов.Записывать = Истина;
	Движение = Движения.ПосещенияАттракционов.Добавить();
	Движение.Период = Дата;
	Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
	Движение.Основание = Ссылка;
	Движение.ВидАттракциона = СвойстваНоменклатуры.ВидАттракциона;
	Движение.КоличествоПосещений = СвойстваНоменклатуры.КоличествоПосещений;
	
	// Регистр ПродажиБилетов
	Движения.ПродажиБилетов.Записывать = Истина;
	Движение = Движения.ПродажиБилетов.Добавить();
	Движение.Период = Дата;
	Движение.ВидАттракциона = СвойстваНоменклатуры.ВидАттракциона;
	Движение.Клиент = Клиент;
	Движение.Сумма = СуммаДокумента;

	ОбработатьБонусныеБаллы(Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИнициализроватьСвойстваНоменклатуры()

	СвойстваНоменклатуры = Новый Структура;
	СвойстваНоменклатуры.Вставить("ВидАттракциона", Справочники.ВидыАттракционов.ПустаяСсылка());
	СвойстваНоменклатуры.Вставить("КоличествоПосещений", 0);
	
	Возврат СвойстваНоменклатуры;
	
КонецФункции
	
Функция СвойстваНоменклатуры()

	СвойстваНоменклатуры = ИнициализроватьСвойстваНоменклатуры();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Номенклатура.ВидАттракциона КАК ВидАттракциона,
		|	Номенклатура.КоличествоПосещений КАК КоличествоПосещений
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СвойстваНоменклатуры, ВыборкаДетальныеЗаписи);
	КонецЕсли;
	
	Возврат СвойстваНоменклатуры;
	
КонецФункции	

// Обработать бонусные баллы.
// 
// Параметры:
//  Отказ - Булево - Отказ
Процедура ОбработатьБонусныеБаллы(Отказ)

	Движения.БонусныеБаллыКлиентов.Записывать = Истина;
	
	Если Не ЗначениеЗаполнено(Клиент) Тогда
		Возврат;	
	КонецЕсли;				
	
	СуммаПокупокКлиента = СуммаПокупокКлиента();
	ДоляНакопленныхБаллов = ДоляНакопленныхБаллов(СуммаПокупокКлиента);
	БаллыКНакоплению = СуммаДокумента * ДоляНакопленныхБаллов / 100;
	
	Если Не БаллыКНакоплению = 0 Тогда
		Движение = Движения.БонусныеБаллыКлиентов.ДобавитьПриход();
		Движение.Период = Дата;
		Движение.Клиент = Клиент;
		Движение.Сумма = БаллыКНакоплению;
	КонецЕсли;	
	
	Если Не БаллыКСписанию = 0 Тогда
		Движение = Движения.БонусныеБаллыКлиентов.ДобавитьРасход();
		Движение.Период = Дата;
		Движение.Клиент = Клиент;
		Движение.Сумма = БаллыКСписанию;
	КонецЕсли;	
	
	// Проверка перерасхода накопленных балов для клиента
	Движения.БонусныеБаллыКлиентов.Записать();
	
	ПерерасходБаллов = ПерерасходБаллов();
	
	Если ПерерасходБаллов > 0 Тогда
		Отказ = Истина;
		
		Поле = "БаллыКСписанию";
		ТекстСообщения = СтрШаблон("Не хватает %1 баллов для списания", ПерерасходБаллов);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Поле);
	КонецЕсли;	 
	
КонецПроцедуры	

// Сумма покупок клиента.
// 
// Возвращаемое значение:
//  Число - Сумма покупок клиента
Функция СуммаПокупокКлиента()
	
	СуммаПокупокКлиента = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПродажиБилетовОбороты.СуммаОборот КАК Сумма
		|ИЗ
		|	РегистрНакопления.ПродажиБилетов.Обороты(, &КонецПериода,, Клиент = &Клиент) КАК ПродажиБилетовОбороты";
	
	Запрос.УстановитьПараметр("КонецПериода", Новый Граница(МоментВремени(), ВидГраницы.Исключая));
	Запрос.УстановитьПараметр("Клиент", Клиент);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		СуммаПокупокКлиента = ВыборкаДетальныеЗаписи.Сумма;
	КонецЕсли;	
	
	Возврат СуммаПокупокКлиента;
	
КонецФункции	

Функция ДоляНакопленныхБаллов(СуммаПокупокКлиента)
	
	ДоляНакопленныхБалов = 0;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	АктуальнаяШкалаБонуснойПрограммыСрезПоследних.Шкала КАК Шкала
		|ПОМЕСТИТЬ ВТАктуальныеШкалы
		|ИЗ
		|	РегистрСведений.АктуальнаяШкалаБонуснойПрограммы.СрезПоследних(&Период,) КАК
		|		АктуальнаяШкалаБонуснойПрограммыСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ШкалаБонуснойПрограммыДиапазоны.ПроцентНакопления КАК Доля
		|ИЗ
		|	ВТАктуальныеШкалы КАК ВТАктуальныеШкалы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ШкалаБонуснойПрограммы.Диапазоны КАК ШкалаБонуснойПрограммыДиапазоны
		|		ПО ВТАктуальныеШкалы.Шкала = ШкалаБонуснойПрограммыДиапазоны.Ссылка
		|ГДЕ
		|	ШкалаБонуснойПрограммыДиапазоны.НижняяГраница <= &СуммаПокупок
		|	И (ШкалаБонуснойПрограммыДиапазоны.ВерхняяГраница > &СуммаПокупок
		|	ИЛИ ШкалаБонуснойПрограммыДиапазоны.ВерхняяГраница = 0)";
		
	Запрос.УстановитьПараметр("СуммаПокупок", СуммаПокупокКлиента);
	Запрос.УстановитьПараметр("Период", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();

	Если Не РезультатЗапроса.Пустой() Тогда	
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		ДоляНакопленныхБалов = ВыборкаДетальныеЗаписи.Доля;		
	КонецЕсли;		
	
	Возврат ДоляНакопленныхБалов;
	
КонецФункции	

Функция ПерерасходБаллов()
	
	Результат = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	БонусныеБаллыКлиентовОстатки.СуммаОстаток КАК Сумма
		|ИЗ
		|	РегистрНакопления.БонусныеБаллыКлиентов.Остатки(&Период, Клиент = &Клиент) КАК БонусныеБаллыКлиентовОстатки
		|ГДЕ
		|	БонусныеБаллыКлиентовОстатки.СуммаОстаток < 0";
	
	Запрос.УстановитьПараметр("Клиент", Клиент);
	Запрос.УстановитьПараметр("Период", Новый Граница(МоментВремени(), ВидГраницы.Включая));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		
		Результат = -ВыборкаДетальныеЗаписи.Сумма;	
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции	

#КонецОбласти

#КонецЕсли